import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
    ext.kotlin_version = '1.6.0'
    ext.ktor_version = '1.6.7'
    ext.koin_version = '3.1.4'
    ext.koin_test = '2.2.2'
    ext.shadow_version = '5.2.0'
    ext.exposed_version = "0.37.3"
    ext.jooq_version = '3.16.4'
    ext.h2Version = "2.1.210"
    ext.hikari_cp_version = "5.0.1"
    ext.junit_version = "5.8.2"
    ext.flyway_version = '8.5.0'
    ext.casbin_version = "1.22.1"
    ext.postgres_jdbc_driver = "42.3.2"

    repositories {
        jcenter()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }

    configurations['classpath'].resolutionStrategy.eachDependency {
        if (requested.group == 'org.jooq') {
            useVersion "$jooq_version"
        }
    }
}

plugins {
    id('org.flywaydb.flyway') version "$flyway_version"
    id("nu.studer.jooq") version "7.1"
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
    id "org.jlleitschuh.gradle.ktlint" version "9.1.1"
    id "jacoco"
    id "org.openapi.generator" version "5.3.0"
}

group 'com.hypto'
version '1.0.0'

wrapper {
    gradleVersion = '6.1.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

mainClassName = "com.hypto.iam.server.Application"
sourceCompatibility = 11

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.kotlin.srcDirs += 'build/generated-src/jooq/main'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams(true)
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
jacoco {
    toolVersion = "0.8.7"
    reportsDir = file("$buildDir/jacoco")
}

shadowJar {
    baseName = 'hypto-iam-server'
    version = '1.0.0'
}

repositories {
    mavenCentral()
    google()
    jcenter()
    maven { url  "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

configurations {
    flywayMigration
}

flyway {
    configurations = ['flywayMigration']
    url = "jdbc:postgresql://localhost:5435/iam"
    user = 'root'
    password = 'password'
}

jooq {
    version = "$jooq_version"
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = flyway.url
                    user = flyway.user
                    password = flyway.password
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        recordVersionFields = 'lock_version' // fields used for optimistic locking
                        recordTimestampFields = 'lock_timestamp' // fields used for optimistic locking
//                        forcedTypes {
//                            forcedType {
//                                name = 'varchar'
//                                includeExpression = '.*'
//                                includeTypes = 'JSONB?'
//                            }
//                            forcedType {
//                                name = 'varchar'
//                                includeExpression = '.*'
//                                includeTypes = 'INET'
//                            }
//                        }

                        // Flags to consider:
                        // - https://www.jooq.org/doc/3.16/manual-single-page/#codegen-generate-visibility-modifier
                        // - https://www.jooq.org/doc/3.16/manual-single-page/#codegen-generate-java-time-types
                    }

                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
//                        embeddablePrimaryKeys = true // Available only in paid version :(
                        daos = true // TODO: make this false before prod deployment
                    }
                    target {
                        packageName = 'com.hypto.iam.server.db'
                    }
                }
            }
        }
    }
}

task generateOpenAPIModels(type: GenerateTask) {
    generatorName = "kotlin-server"
    inputSpec = "$rootDir/iam_openapi_spec.yml".toString()
    outputDir = "$buildDir/openapi-generated-src".toString()
    apiPackage = "com.hypto.iam"
    invokerPackage = "com.hypto.iam"
    modelPackage = "com.hypto.iam.server.models"
    configOptions = [
            serializableModel: "true"
    ]
}

task generateAndMoveOpenAPIModels(dependsOn: [generateOpenAPIModels], type: Copy) {
    delete fileTree(dir: "$rootDir/src/main/kotlin/com/hypto/iam/server/models/aa")
    from("$buildDir/openapi-generated-src/src/main/kotlin/com/hypto/iam/server/models") {
        include '**/*.*'
    }
    destinationDir(new File("$rootDir/src/main/kotlin/com/hypto/iam/server/models"))
    finalizedBy(ktlintFormat)
}

jacocoTestCoverageVerification {
    dependsOn test
    finalizedBy jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
                    "com.hypto.iam.server.db.*",
                    "com.hypto.iam.server.*",
            ]
            limit {
                counter = 'INSTRUCTION'
                value = 'MISSEDRATIO'
                maximum = 0.1
            }
        }
        rule {
            element = 'CLASS'
            excludes = [
                    "com.hypto.iam.server.db.*",
                    "com.hypto.iam.server.*",
            ]
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
        }
    }
}

detekt {
    baseline = file("detekt_baseline.xml")

    // Define the detekt configuration(s) you want to use.
    // Defaults to the default detekt configuration.
    config = files("detekt_config.yml")

    // Applies the config files on top of detekt's default config file. `false` by default.
    buildUponDefaultConfig = true

    autoCorrect = true
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    implementation("io.ktor:ktor-server-netty:$ktor_version")
    implementation("io.ktor:ktor-metrics:$ktor_version")
    implementation("io.ktor:ktor-locations:$ktor_version")
    implementation("io.ktor:ktor-gson:$ktor_version")
    implementation("io.ktor:ktor-client-core:$ktor_version")
    implementation("io.ktor:ktor-client-apache:$ktor_version")
    implementation("io.ktor:ktor-auth:$ktor_version")
    implementation("ch.qos.logback:logback-classic:1.2.10")
    implementation("com.zaxxer:HikariCP:$hikari_cp_version") // DB connection pooling
    implementation("org.flywaydb:flyway-core:$flyway_version") // DB Migrations
    implementation("org.postgresql:postgresql:$postgres_jdbc_driver") // DB JDBC driver

    // JooQ ORM
    implementation("org.jooq:jooq:$jooq_version")
    implementation("org.jooq:jooq-codegen:$jooq_version")
    implementation("org.jooq:jooq-meta:$jooq_version")
    flywayMigration("org.postgresql:postgresql:$postgres_jdbc_driver")
    jooqGenerator("org.postgresql:postgresql:$postgres_jdbc_driver")

    // Casbin
    implementation("org.casbin:jcasbin:$casbin_version") // Policy management

    // Koin dependencies
    implementation "io.insert-koin:koin-ktor:$koin_version"
    implementation "io.insert-koin:koin-logger-slf4j:$koin_version"

    // Logging
    implementation 'io.github.microutils:kotlin-logging:2.1.21'
    implementation 'org.slf4j:slf4j-simple:1.7.36'

    // Metrics
    implementation "io.ktor:ktor-metrics-micrometer:$ktor_version"

    // APM
    implementation 'com.newrelic.telemetry:micrometer-registry-new-relic:0.8.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    // Uncomment the next line if you want to use RSASSA-PSS (PS256, PS384, PS512) algorithms:
    //    runtimeOnly 'org.bouncycastle:bcprov-jdk15on:1.60'
    runtimeOnly 'io.jsonwebtoken:jjwt-gson:0.11.2' // or 'io.jsonwebtoken:jjwt-jackson:0.11.2' for gson

    // Validation
    implementation("io.konform:konform:0.3.0")

    // Test dependencies
    testImplementation "org.koin:koin-test:$koin_test"
    testImplementation "io.insert-koin:koin-test-junit5:$koin_test"
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
    testImplementation("com.h2database:h2:$h2Version")
    testImplementation "io.mockk:mockk:1.12.2"
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
}

// configure jOOQ task such that it only executes when something has changed that potentially affects the generated JOOQ sources
// - the jOOQ configuration has changed (Jdbc, Generator, Strategy, etc.)
// - the classpath used to execute the jOOQ generation tool has changed (jOOQ library, database driver, strategy classes, etc.)
// - the schema files from which the schema is generated and which is used by jOOQ to generate the sources have changed (scripts added, modified, etc.)
tasks.named('generateJooq').configure {
    // ensure database schema has been prepared by Flyway before generating the jOOQ sources
    dependsOn tasks.named('flywayMigrate')

    // declare Flyway migration scripts as inputs on the jOOQ task
    inputs.files(fileTree('src/main/resources/db/migration'))
            .withPropertyName('migrations')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    // make jOOQ task participate in incremental builds (and build caching)
    allInputsDeclared = true
}

compileKotlin.dependsOn clean, generateJooq
